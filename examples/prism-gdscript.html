<h2>Full example</h2>
<pre><code>@icon("res://my_class_icon.png")
class_name MyClass
extends Node3D
## GDScript class example
##
## This class contains sample code for the GDScript language,
## showcasing most of the language's highligthing possibilities.

signal my_signal
signal another_signal(node: Node)

enum CustomAttribute {
	ZERO,
	ONE,
	TWO,
	FIVE = 5,
}

const DEFAULT_LENGTH := 10

@export var editor_variable := 0
@export_range(1, 5) var ranged_variable := 2

var attributes: Dictionary[String, CustomAttribute] = {}
var node_path := ^"my/node/path"
var string_name := &"string_name"

@onready var label: Label = $Path/To/Label
@onready var button: Button = %Button


func _ready() -> void:
	var args := OS.get_cmdline_args()
	if not args.is_empty():
		return
	elif "--headless" in args or editor_variable != 0:
		push_error("Cannot initialize scene")
		return
	move_node(ranged_variable)
	var inner_object := InnerClass.new(CustomAttribute.ZERO)
	print("Initialized object with %s" % [inner_object.inner_var])


func move_node(value: int) -> void:
	position = Vector3.ONE * value * randf()
	# NOTE: Alternative syntax "(%.2f/%+-5.1f/%.2f)" % [position.x, position.y, position.z].
	print("Moved to ({x}/{y}/{z} &lt;{v}>)".format({
		"x": "%.2f" % [position.x],
		"y": "%+-5.1f" % [position.y],
		"z": "%.2f" % [position.z],
		"v": value,
	}))


class InnerClass extends RefCounted:
	var inner_var: Variant = null
	
	func _init(init_var: Variant) -> void:
		inner_var = init_var</code></pre>
